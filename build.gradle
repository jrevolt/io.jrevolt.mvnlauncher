buildscript {
	ext {
		springBootVersion = '1.1.2.BUILD-SNAPSHOT'
	}
	repositories {
		maven { url "$nexusUrl/content/groups/public"; credentials { username="$nexusUser"; password="$nexusPassword"; }}
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'spring-boot'

ext {
	branch = "$git rev-parse --abbrev-ref HEAD".execute().in.text.trim()
	branchShort = branch.replaceFirst(".*/", "")
	commit = "$git rev-parse HEAD".execute().in.text.trim()
	commitShort = commit.substring(0, 7)
	tag = branch.matches("master") ? 'git describe --tags --exact-match'.execute().in.text.trim() : null
	gitDescribe = "$git describe --tags --always --long".execute().in.text.trim()
	snapshotVersion = "${branchShort}-SNAPSHOT"
	releaseVersion = tag ? "${tag}-${commitShort}" : null
	deploymentRepositoryName = releaseVersion ? "releases" : "snapshots"
}

group = "$rootProject.name"
version = "${releaseVersion ?: snapshotVersion}"

sourceCompatibility = 1.7

springBoot {
	mainClass = "io.jrevolt.mvnlauncher.Main"
	layout = "MAVEN"
}

dependencies {
	testCompile("junit:junit:4.11")
}

repositories {
	maven { url "$nexusUrl/content/groups/public"; credentials { username="$nexusUser"; password="$nexusPassword"; }}
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "$nexusUrl/content/repositories/$deploymentRepositoryName") {
				authentication(userName: "$nexusUser", password: "$nexusPassword")
			}
		}
	}
}
